apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version project.version
group project.group


publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/libs/slf4j-jcdp-1.0.0.jar")
            groupId 'com.autoepm'
            artifactId 'slf4j-jcdp'
            version '1.0.0'

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    // Bintray username & api key
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    pkg {
        // Bintray Repository
        repo = 'slf4j-jcdp'
        // Bintray Package
        name = 'slf4j-jcdp'
        // Bintray org
        userOrg = 'autoepm'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/toyg/sl4j-jcdp.git'
        desc = 'SLF4J adapter for JCDP'
        version {
            name = '1.0.0'
            desc = 'SLF4J adapter for JCDP'
            //vcsTag = '1.0.0'
            attributes = ['slf4j-jcdp': 'com.autoepm:slf4j-jcdp:slf4j-jcdp']
            gpg {
                sign = true
                // passphrase for the private key. Keys are uploaded in the JFrog web interface
                passphrase = System.getProperty('bintray.sigpwd')
            }
        }
        // magic to upload jars
        configurations = ['archives']
    }
}
